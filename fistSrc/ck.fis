;fname: ck.fis
;
; CracK monitoring support functions.
;
;============================================================================================================
;************************************************
; Crack Monitoring Package
;
;   Crack data consists of bond breakages, which we denote as cracks. Each crack has: type (contact bonded,
;   parallel bonded, soft bonded, flat jointed or smooth jointed); failure mode (tensile or shear);
;   geometric information (size, position, normal direction and gap); step number at which it formed;
;   and orphan indicator.
;   The numbers of all cracks (partitioned by type and failure mode) are returned by the ck_nX functions.
;   Crack data is stored as a Discrete Fracture Network (DFN). The DFN plot item supports visualization of
;   the cracks. Crack filtering occurs by calling the FISH function ckFilter(g), which selects cracks with
;   a gap less or greater than g. The ckFilter function can be redefined to provide a user-defined filtering
;   criterion. The DFN plot item displays cracks by specifying: {Shape: Fracture}, {Color By: Text Val:
;   group (slot 1 or 2, for all or filtered cracks, respectively)}, and {Color Opt: Colors:
;   select on/off and color for each type and failure mode}.
;
;   Geometric Information:
;     A crack is a disk for the 3D model and a linear segment of unit-thickness depth for the 2D model.
;     A crack has a size (defined in the DFN logic as diameter for the 3D model and length for the 2D model),
;     position, normal direction and gap.
;     * The size is set at creation and then frozen. The size is set to equal:
;         CB: 2*{radius of deformability method},
;         PB: 2*pb_radius,
;         SB: 2*sb_radius,
;         FJ: 2*{element radius (with same area as element)} (3D model)
;             element length (2D model)
;         SJ: 2*sj_radius.
;     * The position, normal direction and gap are updated to correspond with material motion subsequent to
;       bond breakage (see _ckUpdate and ckForceUpdate). If the originating contact exists, then these
;       quantities are obtained from the contact and its associated contact model as described below.

;       If the originating contact no longer exists, then these quantities are set equal to the contact-
;       plane location, contact-plane normal direction and contact gap, respectively, that would be
;       assigned to a contact between the two parent pieces (see _ckGeomNoContact). However, if at least
;       one of the parent pieces no longer exists, then these quantities are frozen at their last updated
;       values, and such cracks are called orphans.
;       If the originating contact exists, then these quantities are set in _ckGeomContact as follows.
;         The crack position is given by
;           x = { x^(e), flat-jointed crack
;               |   x_c, otherwise.
;         The crack normal direction is given by
;           n = { n_j, smooth-jointed crack
;               | n_c, otherwise.
;         The crack gap is given by
;           g = { g_s = g_c - g_r, contact-, parallel- or soft-bonded crack
;               |           g^(e), flat-jointed crack
;               |             g_j, smooth-jointed crack
;
;   Global Variables:
;     ckInited : crack-monitoring package has been initialized (boolean). Use to surround
;                ck-functions when ck.fis may not have been called into PFC --- e.g.,
;                {if ckInited then \nl ckForceUpdate \nl end_if}.
;     ck_nAll, ck_n{CB,PB,SB,FJ,SJ}{t,s} : crack counts
;     ckUpdateRate                       : crack geometry update rate (number of cycles)
;                                          The count is stored in _ckUpdateCnt.
;     ckFilterGap  : display cracks with gap less (ckFilterOpen = 0) or greater
;                    (ckFilterOpen # 0) than this value (used by ckFilter)
;     ckFilterOpen : see ckFilterGap
;
;   Crack Data (stored for each crack):
;     geometry : The size, position and normal direction are stored as DFN data.
;                DFN attributes: 'diameter'/'length' for 3D/2D, 'position' and 'normal'
;     group-1  : name indicating type & failure mode ({CB,PB,SB,FJ,SJ}-{ten,shear}Fail)
;                of all cracks
;     group-2  : name indicating type, failure mode and gap-based filtering criterion
;                ({CB,PB,SB,FJ,SJ}-{ten,shear}Fail(gap < [ckFilterGap])) of filtered cracks
;     extra-1  : originating contact
;     extra-2  : originating element (for flat-jointed cracks only)
;     extra-3  : parent piece-1 (ball or pebble)
;     extra-4  : parent piece-2 (ball, pebble or facet)
;     extra-5  : gap
;     extra-6  : step number at which the crack formed
;     extra-7  : orphan indicator (1: orphaned crack, 0: not an orphaned crack)
;
;   Global Functions:
;     ckInit, ckOn, ckOff, ckForceUpdate, ckListData, ck_nOrphans, ck_nFiltered
;
;   Internal variables/functions:
;     _ckDFN    : pointer to the CrackData-DFN
;     ckGetDFN  : return pointer to the CrackData-DFN
;************************************************
fish define _ckCodeDependencies
; Include functions that will only work with either PFC2D or PFC3D, because they contain Fish intrinsics
; that are specific to each code.
  if global.dim == 3 then
    command
      fish define _ckSize( myCrk, cp, eNum )
        fracture.diameter( myCrk ) = _ckDiam( cp, eNum )
      end
      fish define ckGetSize( myCrk )
        ckGetSize = fracture.diameter( myCrk )
      end
    end_command
  else ; 2D model
    command
      fish define _ckSize( myCrk, cp, eNum )
        fracture.len( myCrk ) = _ckDiam( cp, eNum )
      end
      fish define ckGetSize( myCrk )
        ckGetSize = fracture.len( myCrk )
      end
    end_command
  end_if
end
@_ckCodeDependencies
;************************************************
fish define ckInit
; Initialize the crack-monitoring package, this function can be called multiple times.
;
; OUT: ckInited
;
  if ckInited == false then  ; first call to ckInit
    _ckDFN = dfn.create( 'CrackData' )
  end_if
  _ckResetCrackData
  ckInited = true
  ckOn
end
;------------------------------------------------
fish define ckOn
; Turn on crack monitoring.
  if ckInited == false then
    system.error = 'ckOn: Must call ckInit before calling ckOn.'
  end_if
  if _ckOn then
    exit
  end_if
  ;
  if ckUpdateRate == 0 then ; default
    ckUpdateRate = 2000000  ; two million
  else
    if ckUpdateRate < 1 then
      system.error = 'ckUpdateRate must be greater than or equal to one.'
    end_if
  end_if
  ; ckFilterGap does not have a default, because we allow its value to be zero.
  ;
  command
    fish callback add @_ckBondBreak event bond_break
    fish callback add @_ckUpdate 10.10 ; after EOMs
  end_command
  ;
  _ckOn = true
  ckForceUpdate
end
;------------------------------------------------
fish define ckOff  ; defined above
; Turn off crack monitoring.
  if ckInited == false then
    system.error = 'ckOff: Must call ckInit before calling ckOff.'
  end_if
  if _ckOn == false then
    exit
  end_if
  ;
  command
    fish callback remove @_ckBondBreak event bond_break
    fish callback remove @_ckUpdate 10.10
  end_command
  _ckOn = false
end
;------------------------------------------------
fish define ckListData
; List crack data (type and failure mode of all cracks).
  io.out('## Crack Data --- a crack is a bond breakage [# is "number of"]:')
  if _ckUpdateCnt == 0 then
    io.out('     Geometry (and orphan status) of all cracks IS fully updated.')
  else
    io.out('     Geometry (and orphan status) of all cracks IS NOT fully updated.')
    io.out('       To fully update: @ckForceUpdate.')
  end_if
  io.out('     Type and failure mode of all cracks:')
  io.out('       ck_nAll              (# cracks): '+string(ck_nAll))
  io.out('       ck_nOrphans (# orphaned cracks): '+string(ck_nOrphans))
  io.out('         The position and normal direction of an orphaned crack are frozen at their last updated value.')
  io.out('       ck_nCBt (# contact-bonded  tensile cracks): '+string(ck_nCBt))
  io.out('       ck_nCBs (# contact-bonded  shear   cracks): '+string(ck_nCBs))
  io.out('       ck_nPBt (# parallel-bonded tensile cracks): '+string(ck_nPBt))
  io.out('       ck_nPBs (# parallel-bonded shear   cracks): '+string(ck_nPBs))
  io.out('       ck_nSBt (# soft-bonded     tensile cracks): '+string(ck_nSBt))
  io.out('       ck_nSBs (# soft-bonded     shear   cracks): '+string(ck_nSBs))
  io.out('       ck_nFJt (# flat-jointed    tensile cracks): '+string(ck_nFJt))
  io.out('       ck_nFJs (# flat-jointed    shear   cracks): '+string(ck_nFJs))
  io.out('       ck_nSJt (# smooth-jointed  tensile cracks): '+string(ck_nSJt))
  io.out('       ck_nSJs (# smooth-jointed  shear   cracks): '+string(ck_nSJs))
end
;------------------------------------------------
fish define ckForceUpdate
  if _ckOn then
    _ckUpdateCnt = ckUpdateRate ; force the update on next line
    _ckUpdate
  end_if
end
;------------------------------------------------
fish define ckGetDFN
  if ckInited == true then
    ckGetDFN = _ckDFN
  else
    ckGetDFN = null
  end_if
end
;------------------------------------------------
fish define _ckUpdate
  _ckUpdateCnt = _ckUpdateCnt + 1
  if _ckUpdateCnt >= ckUpdateRate then
    loop foreach local myCrk dfn.fracturelist( _ckDFN )
      _ckGeom( myCrk )
    end_loop
    ckFilter( ckFilterGap )
    _ckUpdateCnt = 0
  end_if
end
;------------------------------------------------
fish define ckFilter( gapIn )
; Filter cracks by adding selected cracks to the group (slot 2) of the CrackData-DFN,
; and providing filtering criterion in the group name (which will be displayed in
; the plot caption). Orphans are excluded from filtered cracks.
; Redefine this function for user-defined filtering.
; Selection Criterion:
;   Crack with gap less (ckFilterOpen == 0) or greater (ckFilterOpen # 0) than gapIn.
;
  local myStr
  if ckFilterOpen == 0 then
    myStr = '(gap < ' + string(gapIn) + ')'
  else
    myStr = '(gap > ' + string(gapIn) + ')'
  end_if
  loop foreach local myCrk dfn.fracturelist( _ckDFN )
    fracture.group( myCrk, 2 ) = 'notFiltered'
    if fracture.extra( myCrk, 7 ) == 0 then  ; not an orphan
      if ckFilterOpen == 0 then
        if fracture.extra( myCrk, 5 ) < gapIn then
          fracture.group( myCrk, 2 ) = fracture.group( myCrk, 1 ) + myStr
        end_if
      else
        if fracture.extra( myCrk, 5 ) > gapIn then
          fracture.group( myCrk, 2 ) = fracture.group( myCrk, 1 ) + myStr
        end_if
      end_if
    end_if
  end_loop
end
;------------------------------------------------
fish define _ckBondBreak( args )
  local cp = args(1) ; originating contact
  local failMode     ; failure mode (1/2 : tensile/shear failure)
  local eNum = 0     ; originating element (for flat-jointed cracks only)
  ;
  if      contact.model(cp) == 'linearcbond' then
    failMode = args(2)
  else if contact.model(cp) == 'linearpbond' then
    failMode = args(2)
  else if contact.model(cp) == 'softbond'    then
    failMode = args(2)
  else if contact.model(cp) == 'flatjoint'   then
    eNum = args(2)
    failMode = args(3)
  else if contact.model(cp) == 'smoothjoint' then
    failMode = args(2)
  else
    system.error = 'Bond-break event cannot be handled by crack-monitoring package.'
  end_if
  ;
 _ckAddCrack( cp, failMode, eNum )
end
;------------------------------------------------
fish define _ckAddCrack( cp, failMode, eNum )
  _ckCountCrack( cp, failMode )
  ;
  local args
  if global.dim == 3 then
    args = array.create(5)
    args(1) = 'disk'              ; fracture type
    args(2) = vector(0.0,0.0,0.0) ; disk position (set in _ckGeom as fracture.pos)
      ; The arg 3 stores disk diameter, which cannot be zero. Use one small value times
      ; x-length of the domain. True disk diameter set in _ckSize as fracture.diameter.
    args(3) = _ftCheckZeroTol * (domain.max(1) - domain.min(1))
    args(4) = 0.0                 ; disk dip      (set in _ckGeom as fracture.normal)
    args(5) = 0.0                 ; disk dipd     (set in _ckGeom as fracture.normal)
  else ; 2D model
    args = array.create(4)
    args(1) = 'vertices'          ; fracture type
    args(2) = 2                   ; number of vertices (must equal 2)
      ; The args 3 & 4 store coords. of vertices.
      ; Cannot simply give (0,0) for both args, so make segment span diagonally across domain.
      ; Then set in _ckSize as fracture.length, and set in _ckGeom as fracture.pos and fracture.normal.
    args(3) = vector( domain.min(1), domain.min(2) )
    args(4) = vector( domain.max(1), domain.max(2) )
  end_if
  local myCrk = fracture.create( _ckDFN, args )
  ;
  fracture.group( myCrk, 1 ) = _ckName( cp, failMode )
  fracture.group( myCrk, 2 ) = 'notFiltered'
  fracture.extra( myCrk, 1 ) = cp
  if contact.model(cp) == 'flatjoint' then
    fracture.extra( myCrk, 2 ) = eNum
  else
    fracture.extra( myCrk, 2 ) = 0 ; not used
  end_if
  fracture.extra( myCrk, 3 ) = contact.end1(cp)
  fracture.extra( myCrk, 4 ) = contact.end2(cp)
  fracture.extra( myCrk, 5 ) = 0.0 ; gap (set in _ckGeom)
  fracture.extra( myCrk, 6) = global.step
  fracture.extra( myCrk, 7) = 0
  _ckSize( myCrk, cp, eNum )
  _ckGeom( myCrk )
end
;------------------------------------------------
fish define _ckName( cp, failMode )
  _ckName = _ckNameType( cp ) + '-' + _ckNameFailMode( failMode ) + 'Fail'
end
;------------------------------------------------
fish define _ckNameType( cp )
  if      contact.model(cp) == 'linearcbond' then
    _ckNameType = 'CB'
  else if contact.model(cp) == 'linearpbond' then
    _ckNameType = 'PB'
  else if contact.model(cp) == 'softbond'    then
    _ckNameType = 'SB'
  else if contact.model(cp) == 'flatjoint'   then
    _ckNameType = 'FJ'
  else if contact.model(cp) == 'smoothjoint' then
    _ckNameType = 'SJ'
  end_if
end
;------------------------------------------------
fish define _ckNameFailMode( failMode )
  if failMode == 1 then
    _ckNameFailMode = 'ten'
  else
    _ckNameFailMode = 'shear'
  end_if
end
;------------------------------------------------
fish define ck_nOrphans
; Return the number of orphaned cracks.
  local nOrphans = 0
  loop foreach local myCrk dfn.fracturelist( _ckDFN )
    if fracture.extra( myCrk, 7 ) == 1 then
      nOrphans = nOrphans + 1
    end_if
  end_loop
  ck_nOrphans = nOrphans
end
;------------------------------------------------
fish define ck_nFiltered
; Return the number of filtered cracks.
  local nFiltered = 0
  loop foreach local myCrk dfn.fracturelist( _ckDFN )
    if fracture.group( myCrk, 2 ) # 'notFiltered' then
      nFiltered = nFiltered + 1
    end_if
  end_loop
  ck_nFiltered = nFiltered
end
;------------------------------------------------
fish define _ckGeom( myCrk )
; Update the orphan status, position, normal direction and gap of myCrk.
;
  fracture.extra( myCrk, 7 ) = 1
  local cp = fracture.extra( myCrk, 1 )
  if cp # null then
    fracture.extra( myCrk, 7 ) = 0
    _ckGeomContact( myCrk, cp )
  else
    local end1 = fracture.extra( myCrk, 3 )
    local end2 = fracture.extra( myCrk, 4 )
    if (end1 # null) & (end2 # null) then
      fracture.extra( myCrk, 7 ) = 0
      _ckGeomNoContact( myCrk, end1, end2 )
    end_if
  end_if
end
;------------------------------------------------
fish define _ckGeomNoContact( myCrk, end1, end2 )
; Set position, normal direction and gap of myCrk equal to the contact-plane location, contact-plane
; normal direction and contact gap, respectively, that would be assigned to a contact between the two
; parent pieces. These three quantities are given by Eq. (2) in {PFC Model Components: Contacts and
; Contact Models: Contact Resolution} of the PFC 5.0 documentation set.
;
  local ckType       ; {0,1} : {grain-grain, grain-facet} crack
  local f2           ; facet at end2, iff ckType == 1
  local pPos1, pPos2 ; position of piece{1,2}
  local pRad1, pRad2 ; radius   of piece{1,2}
  ;
  if type.pointer.id(end1) == ball.typeid then ; end1 is a ball
    pPos1 = ball.pos(end1)
    pRad1 = ball.radius(end1)
  else                                         ; end1 is a pebble
    pPos1 = clump.pebble.pos(end1)
    pRad1 = clump.pebble.radius(end1)
  end_if
  ;
  if      type.pointer.id(end2) == ball.typeid         then ; end2 is a ball
    ckType = 0
    pPos2 = ball.pos(end2)
    pRad2 = ball.radius(end2)
  else if type.pointer.id(end2) == clump.pebble.typeid then ; end2 is a pebble
    ckType = 0
    pPos2 = clump.pebble.pos(end2)
    pRad2 = clump.pebble.radius(end2)
  else                                                      ; end2 is a facet
    ckType = 1
    f2 = end2
  end_if
  ;
  ; Compute {_xc, _nc, _gc}, and store at end of this section.
  local _d, _nc, _gc
  if ckType == 0 then ; grain-grain crack
    _d = math.mag( pPos2 - pPos1 )
    _gc = _d - ( pRad1 + pRad2 )
    if math.abs(_d) > 1e-6*pRad1 then
      _nc = ( pPos2 - pPos1 ) / _d
    else
      _nc = vector( 0.0, 0.0, 1.0 ) ; excessive grain-grain overlap, punt
    end_if
  else                ; grain-facet  crack
    local _xf = wall.facet.pointnear( f2, pPos1 )
    _d = math.mag( _xf - pPos1 )
    _gc = _d - pRad1
    if math.abs(_d) > 1e-6*pRad1 then
      _nc = ( _xf - pPos1 ) / _d
    else
      _nc = vector( 0.0, 0.0, 1.0 ) ; excessive ball-facet overlap, punt
    end_if
  end_if
  local _xc = pPos1 + ( pRad1 + 0.5*_gc )*_nc
  ;
  fracture.pos(    myCrk )    = _xc
  fracture.normal( myCrk )    = _nc
  fracture.extra(  myCrk, 5 ) = _gc
end
;------------------------------------------------
fish define _ckGeomContact( myCrk, cp )
; Set position, normal direction and gap of myCrk equal to the values given in the Crack Monitoring
; section of the fistPkg memo.
  local _xc
  if contact.model(cp) == 'flatjoint' then
    local eNum = fracture.extra( myCrk, 2 )
    contact.prop( cp, 'fj_elem' ) = eNum
    _xc = contact.prop( cp, 'fj_cen' )
  else ; not a flat-jointed crack
    _xc = contact.pos( cp )
  end_if
  ;
  local _nc
  if contact.model(cp) == 'smoothjoint' then
    _nc = contact.prop( cp, 'sj_unorm' )
  else ; not a smooth-jointed crack
    _nc = contact.normal( cp )
  end_if
  ;
  local _gc
  if    (contact.model(cp) == 'linearcbond') ...
      | (contact.model(cp) == 'linearpbond') ...
      | (contact.model(cp) == 'softbond'   ) then
    _gc = contact.gap(cp) - contact.prop( cp, 'rgap' )
  else if contact.model(cp) == 'flatjoint'   then
    _gc = contact.prop( cp, 'fj_egap' ) ; 'fj_elem' was set above, while obtaining _xc
  else if contact.model(cp) == 'smoothjoint' then
    _gc = contact.prop( cp, 'sj_gap' )
  else
    system.error = '_ckGeomContact: cannot handle this contact type.' ; should not occur
  end_if
  ;
  fracture.pos(    myCrk )    = _xc
  fracture.normal( myCrk )    = _nc
  fracture.extra(  myCrk, 5 ) = _gc
end
;------------------------------------------------
fish define _ckBallBall( end2 )
  if      type.pointer.id(end2) == ball.typeid         then ; end2 is a ball
    _ckBallBall = true
  else if type.pointer.id(end2) == clump.pebble.typeid then ; end2 is a pebble
    _ckBallBall = true
  else                                                      ; end2 is a facet
    _ckBallBall = false
  end_if
end
;------------------------------------------------
fish define _ckDiam( cp, eNum )
  if      contact.model(cp) == 'linearcbond' then
    _ckDiam = 2.0 * _ckDeformabilityRadius( cp )
  else if contact.model(cp) == 'linearpbond' then
    _ckDiam = 2.0 * contact.prop( cp, 'pb_radius' )
  else if contact.model(cp) == 'softbond' then
    _ckDiam = 2.0 * contact.prop( cp, 'sb_radius' )
  else if contact.model(cp) == 'flatjoint'   then
    contact.prop( cp, 'fj_elem' ) = eNum
    if global.dim == 3 then
      _ckDiam = 2.0 * math.sqrt( contact.prop(cp, 'fj_area') / math.pi )
    else ; 2D model
      _ckDiam = contact.prop(cp, 'fj_area') ; A = 2Rt/Nr (element length)
    end_if
  else if contact.model(cp) == 'smoothjoint' then
    _ckDiam = 2.0 * contact.prop( cp, 'sj_radius' )
  end_if
end
;------------------------------------------------
fish define _ckDeformabilityRadius( cp )
; Return radius of deformability method of linear-based models.
;
  local b1, b2, p1, p2
  if      type.pointer.id(cp) == contact.typeid('ball-ball')     then
    b1 = contact.end1(cp)
    b2 = contact.end2(cp)
    _ckDeformabilityRadius = math.min( ball.radius(b1), ball.radius(b2) )
  else if type.pointer.id(cp) == contact.typeid('ball-pebble')   then
    b1 = contact.end1(cp)
    p2 = contact.end2(cp)
    _ckDeformabilityRadius = math.min( ball.radius(b1), clump.pebble.radius(p2) )
  else if type.pointer.id(cp) == contact.typeid('pebble-pebble') then
    p1 = contact.end1(cp)
    p2 = contact.end2(cp)
    _ckDeformabilityRadius = math.min( clump.pebble.radius(p1), clump.pebble.radius(p2) )
  else if type.pointer.id(cp) == contact.typeid('ball-facet')    then
    b1 = contact.end1(cp)
    _ckDeformabilityRadius = ball.radius(b1)
  else if type.pointer.id(cp) == contact.typeid('pebble-facet')  then
    p1 = contact.end1(cp)
    _ckDeformabilityRadius = clump.pebble.radius(p1)
  end_if
end
;------------------------------------------------
fish define _ckCountCrack( cp, failMode )
; Update the crack counts.
;
  ck_nAll = ck_nAll + 1
  ;
  if      contact.model(cp) == 'linearcbond' then
    if failMode == 1 then
      ck_nCBt = ck_nCBt + 1
    else
      ck_nCBs = ck_nCBs + 1
    end_if
  else if contact.model(cp) == 'linearpbond' then
    if failMode == 1 then
      ck_nPBt = ck_nPBt + 1
    else
      ck_nPBs = ck_nPBs + 1
    end_if
  else if contact.model(cp) == 'softbond' then
    if failMode == 1 then
      ck_nSBt = ck_nSBt + 1
    else
      ck_nSBs = ck_nSBs + 1
    end_if
  else if contact.model(cp) == 'flatjoint'   then
    if failMode == 1 then
      ck_nFJt = ck_nFJt + 1
    else
      ck_nFJs = ck_nFJs + 1
    end_if
  else if contact.model(cp) == 'smoothjoint' then
    if failMode == 1 then
      ck_nSJt = ck_nSJt + 1
    else
      ck_nSJs = ck_nSJs + 1
    end_if
  end_if
end
;------------------------------------------------
fish define _ckResetCrackData
; Reset crack data by: zeroing the global crack variables, and deleting all cracks.
;
  ; Zero the crack counts.
  ck_nAll = 0
  ck_nCBt = 0
  ck_nCBs = 0
  ck_nPBt = 0
  ck_nPBs = 0
  ck_nSBt = 0
  ck_nSBs = 0
  ck_nFJt = 0
  ck_nFJs = 0
  ck_nSJt = 0
  ck_nSJs = 0
  ;
  ; Delete all cracks.
  loop foreach local myCrk dfn.fracturelist( _ckDFN )
    fracture.delete( myCrk )
  end_loop
end
program return
;EOF: ck.fis